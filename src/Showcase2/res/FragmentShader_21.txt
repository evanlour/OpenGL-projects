#version 330 core

struct Material
{
	vec3 ambient_color;
	vec3 diffuse_color;
	vec3 specular_color;
	float shininess;
};

uniform Material material;

struct Light_source
{
    int type; //0 for Point Light, 1 For directional Light, 2 to not use the light at all
	vec3 direction; // only for Directional Light
	vec3 position;

	vec3 ambient_color;
	vec3 diffuse_color;
	vec3 specular_color;

	float constant;
	float linear;
	float quadratic;
};

uniform Light_source light_sources[6];

in vec3 normal;
in vec3 frag_pos;

uniform vec3 camera_position;

void main()
{
    vec3 result = vec3(0.0);
    for(int i = 0; i < 6; i++){
        Light_source light = light_sources[i];

        vec3 lightDir;
        float attenuation = 1.0;

        if (light.type == 1) {
            lightDir = normalize(-light.direction);
        } else {
            lightDir = normalize(light.position - frag_pos);
            float dist = length(light.position - frag_pos);
            attenuation = 1.0 / (light.constant + light.linear * dist + light.quadratic * dist * dist);
        }

        vec3 ambient = light.ambient_color * material.ambient_color;

        float diff = max(dot(normal, lightDir), 0.0);
        vec3 diffuse = diff * light.diffuse_color * material.diffuse_color;

        vec3 viewDir = normalize(camera_position - frag_pos);
        vec3 reflectDir = reflect(-lightDir, normal);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
        vec3 specular = spec * light.specular_color * material.specular_color;

        ambient *= attenuation;
        diffuse *= attenuation;
        specular *= attenuation;

        if(light.type != 2){
            result += ambient + diffuse + specular;
        }
    }

    gl_FragColor = vec4(result, 1.0);
}
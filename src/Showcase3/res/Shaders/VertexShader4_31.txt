#version 330 core

layout (location = 0) in vec3 inputPosition;
layout (location = 1) in vec3 inputNormal;
layout (location = 2) in vec2 inputTextureCoordinates;
layout (location = 3) in vec3 inputTangent;
layout (location = 4) in vec3 inputBitangent;

#define MAX_LIGHTS 8

out VertexOutput {
    vec3 fragmentPosition;
    vec2 textureCoordinates;
    vec3 tangentLightSourcePosition[MAX_LIGHTS];
    vec3 tangentLightSourceDirection[MAX_LIGHTS];
    vec3 tangentViewPosition;
    vec3 tangentFragmentPosition;
} vertexOutput;

uniform mat4 projection;
uniform mat4 view;
uniform mat4 model;

uniform int point_lights;
uniform int dir_lights;
uniform vec3 lightSourcePosition[MAX_LIGHTS];
uniform vec3 lightSourceDirection[MAX_LIGHTS];
uniform vec3 camera_position;

void main()
{
    vertexOutput.fragmentPosition = vec3(model * vec4(inputPosition, 1.0));   
    vertexOutput.textureCoordinates = inputTextureCoordinates;
    
    mat3 normalMatrix = transpose(inverse(mat3(model)));
    vec3 T = normalize(normalMatrix * inputTangent);
    vec3 N = normalize(normalMatrix * inputNormal);
    T = normalize(T - dot(T, N) * N);
    vec3 B = cross(N, T);
    
    mat3 TBN = transpose(mat3(T, B, N));
    int delimiter = (point_lights < MAX_LIGHTS) ? point_lights : MAX_LIGHTS;
    for(int i = 0; i < delimiter; i++){
        vertexOutput.tangentLightSourcePosition[i] = TBN * lightSourcePosition[i];
    }
    delimiter = (dir_lights < MAX_LIGHTS) ? dir_lights : MAX_LIGHTS;
    for(int i = 0; i < delimiter; i++){
        vertexOutput.tangentLightSourceDirection[i] = TBN * lightSourceDirection[i];
    }
    vertexOutput.tangentViewPosition  = TBN * camera_position;
    vertexOutput.tangentFragmentPosition  = TBN * vertexOutput.fragmentPosition;
        
    gl_Position = projection * view * model * vec4(inputPosition, 1.0);
}

